class NmdPlot.Map
  options:
    worldCenter:    new L.LatLng(37.3002752813443, 5.625)
    indiaCenter:    new L.LatLng(21.759499730719817, 90.263671875)
    indiaZoomLevel: 5
    worldZoomLevel: 2
    cloudMade:
      apiKey:  "fe1dbd920bce412faf852270ad2ee911"
      styleId: "107987"


  constructor: (containerId)->
    L.Icon.Default.imagePath = "/"
    @lmap = L.map('map')
    @addCloudMadeLayer()
    @lmap.setView(@options.indiaCenter, @options.indiaZoomLevel)
    @markerLayer = L.layerGroup([])
    @placeLayer  = L.layerGroup([])
    @lmap.addLayer(@markerLayer)
    @lmap.addLayer(@placeLayer)
    @lmap.on('zoomend', @onZoomend);
    @


  onZoomend: ()=>
    if(@lmap.getZoom()>=5 && !@lmap.hasLayer(@placeLayer))
      @lmap.addLayer(@placeLayer)
      for key, value of @placeLayer._layers
        @placeLayer._layers[key].showLabel()

    if(@lmap.getZoom()<5 && @lmap.hasLayer(@placeLayer))
      @lmap.removeLayer(@placeLayer)


  setDefaultIconOnMarkers: (markerIds)->
    for markerId in markerIds
      @lmap._layers[markerId].setZIndexOffset(-50)
      if @markerLayer._layers[markerId]._map.hasLayer(@markerLayer._layers[markerId]._popup)
        @markerLayer._layers[markerId].closePopup()

      @markerLayer._layers[markerId].setIcon(@defaultIcon(@markerLayer._layers[markerId].weight))


  highlightMarker: (markerId)->
    @lmap._layers[markerId].setZIndexOffset(50)
    @lmap._layers[markerId].setIcon(@highlightIcon(@lmap._layers[markerId].weight))


  addOSMLayer: ->
    osm = new L.TileLayer(
      'http://{s}.tile.openstreetmap.org/{z}/{x}/{y}.png',
       minZoom: 2,
       maxZoom: 15,
       attribution: 'Map data &copy; OpenStreetMap contributors'
    )
    @lmap.addLayer(osm)


  addCloudMadeLayer: ->
    cloudmade = L.tileLayer(
      'http://{s}.tile.cloudmade.com/{key}/{styleId}/256/{z}/{x}/{y}.png',
      attribution: 'Map data &copy; 2011 OpenStreetMap contributors, Imagery &copy; 2011 CloudMade',
      key: @options.cloudMade.apiKey,
      styleId: @options.cloudMade.styleId
    ).addTo(@lmap)


  createPlaceLabel: (label, opts)->
    latlng = new L.LatLng(opts.lat, opts.lng)
    marker = L.marker(latlng, icon: @invisibleIcon())
    marker.setZIndexOffset(-100)
    marker.bindLabel(label, noHide: true)
      .addTo(@placeLayer)
      .showLabel()


  createMarker: (markup, opts) ->
    latlng = new L.LatLng(opts.lat, opts.lng)
    marker = L.marker(latlng, icon: @defaultIcon(opts.weight))
      .bindPopup(markup, {className: 'marker-popup'})

    marker.weight = opts.weight

    if opts.labelId?
      marker.groupLabelId = opts.labelId

    marker._leaflet_id = opts.leafletId if opts.leafletId
    marker.on "popupopen", =>
      marker.setZIndexOffset(50)

    marker.on "click", ->
      if marker.groupLabelId?
        $("##{marker.groupLabelId}").click()

    marker.on "popupclose", =>
      # marker.setIcon(@defaultIcon(marker.weight))
      marker.setZIndexOffset(-50)

    @markerLayer.addLayer(marker)


  clear: ->
    @markerLayer.clearLayers()
    @placeLayer.clearLayers()


  highlightIcon: (size = 1)->
    size = size/2 if size > 1
    iconSize   = 8 * size
    anchorSize = iconSize/2
    L.icon(
      iconUrl: "<%= asset_path('red-icon.png') %>",
      iconSize:     [iconSize, iconSize], # size of the icon
      iconAnchor:   [anchorSize, anchorSize], # point of the icon which will correspond to marker's location
      popupAnchor:  [0, 0] # point from which the popup should open relative to the iconAnchor
    )


  invisibleIcon: ()->
    L.icon(
      iconUrl: "<%= asset_path('transparent-marker-icon.png') %>",
      iconSize:     [8, 8], # size of the icon
      iconAnchor:   [2, 2], # point of the icon which will correspond to marker's location
      popupAnchor:  [0, 0]  # point from which the popup should open relative to the iconAnchor
    )


  defaultIcon: (size = 1)->
    size = size/2 if size > 1
    iconSize   = 8 * size
    anchorSize = iconSize/2
    L.icon(
      iconUrl: "<%= asset_path('marker-icon.png') %>",
      iconSize:     [iconSize, iconSize], # size of the icon
      iconAnchor:   [anchorSize, anchorSize], # point of the icon which will correspond to marker's location
      popupAnchor:  [0, 0] # point from which the popup should open relative to the iconAnchor
    )
