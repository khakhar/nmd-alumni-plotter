class NmdPlot.Plotters.Internship extends NmdPlot.Plotters.Base

  populateNavList: ()->
    # sort backgrounds by name
    for backgroundId in @sortedIds(@tmpGroups, @backgroundSorter)
      $background = $("<div class='background-name'>").html(@getBackground(backgroundId))
      $(".nav-list .items").append($background)

      orgIdsForBackground = []
      for orgId, placeGroups of @tmpGroups[backgroundId]
        orgIdsForBackground.push(orgId)

      for orgId in orgIdsForBackground.sort(@organisationSorter)
        organisation = @getOrganisation(orgId)
        $orgLabel = $("<span>")
          .attr(id: @labelId(backgroundId, orgId))
          .data('unique-ids', [backgroundId, orgId])
          .html(organisation.name)
        $item = $("<div>").addClass("item").append($orgLabel)

        if organisation.website?
          $itemLink  = $("<a>")
            .attr(
              href: organisation.website
              target: "_blank"
            ).html($("<img>").attr('src', '<%= asset_path('arrow.png') %>'))
          $item.append($itemLink)

        $orgLabel.click @labelOnClick

        $students = $("<div>").addClass("students")
        for placeId, studentIds of @tmpGroups[backgroundId][orgId]
          for studentId in studentIds
            student = @getStudent(studentId)
            $student = $("<div>")
              .addClass("student")
              .html("- ")
            $student.append($("<a>").attr('href', student.website).html(student.name))
            $students.append($student)

        $item.append($students)
        $(".nav-list .items").append($item)



  labelOnClick: (event)=>
    uniqueIds = $(event.target).data("unique-ids")

    $(".item").removeClass("selected-item")
    $(event.target).parent().addClass("selected-item")

    @view.geo.setDefaultIconOnMarkers(@previousMarkerIds)
    for placeId, studentIds of @currentGroups[uniqueIds[0]][uniqueIds[1]]
      @view.geo.highlightMarker(@markerId(uniqueIds[0], uniqueIds[1], placeId))
      @previousMarkerIds.push @markerId(uniqueIds[0], uniqueIds[1], placeId)


  createMarker: (studentIds, opts) ->
    markup = ""

    for studentId in studentIds
      student = @getStudent(studentId)
      markup += """
        <div class="student">
          <div class='student-name'>#{student.name}</div>
          <div class='project-title'>#{student.project_title}</div>
        </div>
      """
    opts['weight'] = studentIds.length
    @view.geo.createMarker markup, opts


  groupData: ->
    for studentId, student of @data.students
      continue if !student.internship_place_id

      backgroundId = student.background_id
      orgId   = student.internship_organisation_id
      placeId = student.internship_place_id

      if !@currentGroups[backgroundId]
        @currentGroups[backgroundId] = {}

      if !@currentGroups[backgroundId][orgId]
        @currentGroups[backgroundId][orgId] = {}

      if !@currentGroups[backgroundId][orgId][placeId]
        @currentGroups[backgroundId][orgId][placeId] = []
        @addPlace(placeId)

      @currentGroups[backgroundId][orgId][placeId].push(studentId)

    @tmpGroups = @currentGroups


  # Trying to filter using currentGroups would be a nightmare for cleaning up data
  filterGroups: (string)->
    for studentId, student of @data.students
      continue if !student.internship_place_id

      organisation = @getOrganisation(student.internship_organisation_id)
      continue if !organisation.name.match(new RegExp(string, "gi"))

      backgroundId = student.background_id
      orgId   = student.internship_organisation_id
      placeId = student.internship_place_id

      if !@tmpGroups[backgroundId]
        @tmpGroups[backgroundId] = {}

      if !@tmpGroups[backgroundId][orgId]
        @tmpGroups[backgroundId][orgId] = {}

      if !@tmpGroups[backgroundId][orgId][placeId]
        @tmpGroups[backgroundId][orgId][placeId] = []
        @addPlace(placeId)

      @tmpGroups[backgroundId][orgId][placeId].push(studentId)


  plot: (options={})->
    @resetState(options)

    if !options.filter
      @groupData()
    else
      @filterGroups(options.filterString)

    for backgroundId, orgGroups of @tmpGroups
      for orgId, placeGroups of orgGroups
        for placeId, studentIds of placeGroups
          place   = @getPlace(placeId)
          marker  = @createMarker(
            studentIds,
            lat: @getPlace(placeId).latitude
            lng: @getPlace(placeId).longitude
            labelId: @labelId(backgroundId, orgId)
            leafletId: @markerId(backgroundId, orgId, placeId)
          )

    @populateNavList()
